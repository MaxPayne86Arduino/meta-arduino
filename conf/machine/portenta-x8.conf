#@TYPE: Machine
#@NAME: Arduino Portenta-X8 i.MX8MM
#@SOC: i.MX8MM
#@DESCRIPTION: Machine configuration for Arduino Potenta-X8 (i.MX 8M Mini + STM32H7)
#@MAINTAINER: Arduino <info@arduino.cc>

require conf/machine/imx8mm-lpddr4-evk.conf

MACHINEOVERRIDES .= ":imx8mm-lpddr4-evk"

# Override the variables you want to change here
UBOOT_CONFIG_BASENAME = "portenta-x8"
UBOOT_DTB_NAME = "portenta-x8.dtb"

IMX_DEFAULT_BSP = "nxp"
PREFERRED_PROVIDER:virtual/kernel = "linux-imx_6.6"
#KERNEL_DEFCONFIG = "portenta_x8_defconfig"
KBUILD_DEFCONFIG:portenta-x8 = "portenta_x8_defconfig"
KERNEL_DEVICETREE = ""
KERNEL_DEVICETREE:remove:portenta-x8 = " freescale/imx8mm-evk.dtb"
PREFERRED_PROVIDER:virtual/dtb ?= "arduino-device-tree"

# Include custom device tree for linux kernel
MACHINE_EXTRA_RRECOMMENDS += "arduino-device-tree"

# Add additional firmware, mostly support for Murata 1DX WiFi / Bluetooth solution
MACHINE_FIRMWARE:append = " linux-firmware linux-firmware-cyw-fmac-fw linux-firmware-cyw-fmac-nvram linux-firmware-cyw-bt-patch"

# Added X8H7 solution based on ext. STM32 mcu
MACHINE_FIRMWARE:append = " linux-firmware-arduino-portenta-x8-stm32h7"

# Add custom video bridge and usb typec controller / power delivery solution
MACHINE_EXTRA_RRECOMMENDS += "anx7625"

# Add x8h7 driver for communication with external cortex processor via spi
MACHINE_EXTRA_RRECOMMENDS += "x8h7"

# Add extra kernel modules needed for Max carrier board
MACHINE_EXTRA_RRECOMMENDS += "bq24195 cs42l52"

# Add mipi-csi cmos sensors drivers
MACHINE_EXTRA_RRECOMMENDS += "ov5647-mipi imx219 imx477 imx708"

# Add mipi-dsi panels and touch screen controllers drivers
MACHINE_EXTRA_RRECOMMENDS += "panel-sitronix-st7701 panel-jadard-ek79202d atmel-mxt-ts"

# Add neural external pcie modules drivers
MACHINE_EXTRA_RRECOMMENDS += "google-gasket-driver akida-pcie-driver hailo-pcie-driver"

# Module to load devicetree overlays at runtime in userspace
MACHINE_EXTRA_RRECOMMENDS:append = " dtbocfg"

# Portenta X8 has a Murata 1DX wifi/bt module which uses custom recipes
# linux-firmware-cyw-fmac-fw, linux-firmware-cyw-fmac-nvram and linux-firmware-cyw-bt-patch.
# Make sure we avoid default linux bcm43430 firmware
CORE_IMAGE_BASE_INSTALL:remove:portenta-x8 = " \
    linux-firmware-bcm43430 \
"

KERNEL_MODULE_AUTOLOAD:portenta-x8:append = "anx7625 i2c-dev spidev"

# @TODO: below seems to be ignored
SERIAL_CONSOLES = "115200;ttymxc2"
